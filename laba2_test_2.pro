

domains
  имя = а; б; в; г; д
  цвет = белый; черный

  
  мальчик = наклеено(имя, цвет)
  
  количество =  integer  %белых(integer); черных(integer)
  %               белые                  черные
  видит = сколько(количество, количество)
  
  гипотеза = мальчик*
  имена = имя*
PREDICATES
  nondeterm возможный_цвет(цвет)
  nondeterm возможное_имя(имя)
  
  nondeterm входит_в(мальчик, гипотеза)
  nondeterm входит_в(имя, имена)
  nondeterm длина_списка(имена, integer)
  
  nondeterm генерация_гипотезы(гипотеза)
  
  nondeterm сказал(имя, видит)
  
  nondeterm что_видит(имя, мальчик, гипотеза)
  nondeterm сколько_видит_кружков_цвета(имя, цвет, гипотеза, integer)
  
  nondeterm проверка_высказывания(имя, гипотеза)
  nondeterm проверка_правдивости(имя, гипотеза)
  nondeterm проверка_высказываний(гипотеза)
  
  nondeterm поиск_решения(гипотеза)
CLAUSES

  длина_списка([], 0). % для завершения рекурсии
  длина_списка([_|Остальные], Длина):-
    длина_списка(Остальные, ДлинаОстальных),
    Длина = ДлинаОстальных + 1.
    
  входит_в(Элемент, [Элемент|_]).
  входит_в(Элемент, [_|ОстальныеЭлементы]):-
    входит_в(Элемент, ОстальныеЭлементы).

 % возможный_цвет(белый).
  %возможный_цвет(черный).
  возможный_цвет(Цвет):-
    Цвет = белый;
    Цвет = черный.
  
 % возможное_имя(а).
 % возможное_имя(б).
 % возможное_имя(в).
 % возможное_имя(г).
 % возможное_имя(д).
    возможное_имя(Имя):-
        Имя = а;
        Имя = б;
        Имя = в;
        Имя = г;
        Имя = д.
  
  генерация_гипотезы(Гипотеза):-
    
    возможный_цвет(Цвет_А), 
    возможный_цвет(Цвет_Б), 
    возможный_цвет(Цвет_В),
    возможный_цвет(Цвет_Г),
    возможный_цвет(Цвет_Д),
    Гипотеза = [
        наклеено(а, Цвет_А), 
        наклеено(б, Цвет_Б), 
        наклеено(в, Цвет_В), 
        наклеено(г, Цвет_Г), 
        наклеено(д, Цвет_Д)
      ].
    
  что_видит(Имя, наклеено(ДругоеИмя, Цвет), Гипотеза):-
    входит_в(наклеено(ДругоеИмя, Цвет), Гипотеза),
    NOT(ДругоеИмя = Имя). % не видит сам себя
    
  сколько_видит_кружков_цвета(Имя, Цвет, Гипотеза, Сколько):-
    findall(ДругоеИмя, что_видит(Имя, наклеено(ДругоеИмя, Цвет), Гипотеза), Имена),
    длина_списка(Имена, Сколько).
  
  сказал(а, сколько(3, 1)).
  сказал(б, сколько(0, 4)).
  сказал(в, сколько(1, 3)).
  сказал(д, сколько(4, 0)).
   
   проверка_высказывания(Имя, Гипотеза):-
     сказал(Имя, сколько(КоличествоБелых, КоличествоЧерных)),
     сколько_видит_кружков_цвета(Имя, белый, Гипотеза, КоличествоБелых),
     сколько_видит_кружков_цвета(Имя, черный, Гипотеза, КоличествоЧерных).
     
   % высказывания белых - правда, а черных - ложь
   проверка_правдивости(Имя, Гипотеза):-
     входит_в(наклеено(Имя, белый), Гипотеза),
     проверка_высказывания(Имя, Гипотеза);
     входит_в(наклеено(Имя, черный), Гипотеза),
     NOT(проверка_высказывания(Имя, Гипотеза)).
     
   проверка_высказываний(Гипотеза):-
     проверка_правдивости(а, Гипотеза),
     проверка_правдивости(б, Гипотеза),
     проверка_правдивости(в, Гипотеза),
     проверка_правдивости(д, Гипотеза).
     
   поиск_решения(Гипотеза):-
    генерация_гипотезы(Гипотеза),
    проверка_высказываний(Гипотеза).
goal
  поиск_решения(Гипотеза).
    



